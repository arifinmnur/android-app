import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.akaita.android.easylauncher'
apply plugin: 'com.google.firebase.firebase-perf'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

static buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def gitHash() {
  return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}

def version = "2.0.0"
def version_code = 114

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'

  dataBinding {
    enabled = true
  }

  defaultConfig {
    applicationId "com.kelsos.mbrc"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode version_code
    versionName version
    buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

    // Enabling multidex support.
    multiDexEnabled true

    testInstrumentationRunner 'com.kelsos.mbrc.MockTestRunner'
    testApplicationId "com.kelsos.mbrc.test"
    vectorDrawables.useSupportLibrary = true
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      returnDefaultValues = true
    }

    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  signingConfigs {
    release {
      storeFile file('placeholder')
      keyAlias "placeholder"
      storePassword "placeholder"
      keyPassword "placeholder"
    }
  }

  flavorDimensions "basic"

  buildTypes {
    release {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
      testCoverageEnabled true

      buildConfigField "String", "GIT_SHA", "\"debug_build\""
      buildConfigField "String", "BUILD_TIME", "\"debug_build\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "false"
    }
  }

  productFlavors {
    play {
      ext.enableCrashlytics = true
    }

    github {
      ext.enableCrashlytics = false
    }
  }


  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    abortOnError false
    lintConfig file("lint.xml")
    xmlReport false
    htmlReport true
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }

  dexOptions {
    javaMaxHeapSize "2G"
  }

  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${defaultConfig.applicationId}-${variant.flavorName}-${variant.versionCode}-v${variant.versionName}.apk"
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

dependencies {
  //Instrumentation Test dependencies
  androidTestImplementation 'androidx.annotation:annotation:1.0.2'

  androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'
  androidTestImplementation 'androidx.test:runner:1.1.1'
  androidTestImplementation 'androidx.test.ext:junit:1.1.0'
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
  androidTestImplementation 'androidx.test.ext:truth:1.1.0'

  androidTestUtil 'androidx.test:orchestrator:1.1.1'

  androidTestImplementation "io.mockk:mockk-android:1.9.1"
  androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.3"

  androidTestImplementation 'com.linkedin.testbutler:test-butler-library:2.0.0'
  androidTestUtil 'com.linkedin.testbutler:test-butler-app:2.0.0@apk'

  // Test dependencies
  testImplementation 'org.koin:koin-test:1.0.2'

  testImplementation "androidx.test:runner:1.1.1"
  testImplementation "androidx.test.ext:junit:1.1.0"
  testImplementation "androidx.test.espresso:espresso-intents:3.1.1"
  testImplementation "androidx.test.espresso:espresso-core:3.1.1"
  testImplementation "androidx.test.ext:truth:1.1.0"
  testImplementation "android.arch.core:core-testing:1.1.1"
  testImplementation "android.arch.persistence.room:testing:1.1.1"

  testImplementation "io.mockk:mockk:1.9.1"

  testImplementation "org.robolectric:robolectric:4.2"
  testImplementation "org.robolectric:shadows-multidex:4.2"

  testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.3"
  //The android library only will fail the robolectric tests
  testImplementation 'org.threeten:threetenbp:1.3.8'

  implementation 'com.github.anrwatchdog:anrwatchdog:1.3.0'

  implementation 'com.chibatching.kotpref:kotpref:2.6.0'

  implementation 'com.google.android.material:material:1.1.0-alpha03'
  implementation 'androidx.databinding:databinding-runtime:3.5.0-alpha06'

  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'androidx.media:media:1.0.1'

  implementation 'androidx.core:core:1.1.0-alpha04'
  implementation 'androidx.core:core-ktx:1.0.1'

  implementation 'androidx.annotation:annotation:1.0.2'
  implementation 'androidx.recyclerview:recyclerview:1.0.0'
  implementation 'androidx.preference:preference:1.1.0-alpha03'

  implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-rc01'
  implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-rc01'

  implementation 'androidx.paging:paging-runtime:2.1.0'

  implementation 'androidx.room:room-runtime:2.1.0-alpha04'
  implementation 'androidx.room:room-rxjava2:2.1.0-alpha04'
  kapt "androidx.room:room-compiler:2.1.0-alpha04"

  implementation 'com.squareup.moshi:moshi:1.8.0'
  kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'

  implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

  implementation 'org.koin:koin-android:1.0.2'
  implementation 'org.koin:koin-androidx-scope:1.0.2'
  implementation 'org.koin:koin-androidx-viewmodel:1.0.2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.1.1"

  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'androidx.multidex:multidex:2.0.1'

  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  implementation 'com.jakewharton.threetenabp:threetenabp:1.1.2'

  def arrow_version = "0.8.2"
  implementation "io.arrow-kt:arrow-core:$arrow_version"
  implementation "io.arrow-kt:arrow-syntax:$arrow_version"
  implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"
  implementation "io.arrow-kt:arrow-data:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-core:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-data:$arrow_version"
  kapt "io.arrow-kt:arrow-annotations-processor:$arrow_version"

  debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.3"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.3"

  debugImplementation 'com.facebook.stetho:stetho:1.5.0'

  playImplementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
    transitive = true
  }
  playImplementation 'com.google.firebase:firebase-perf:16.2.3'
  playImplementation 'com.google.firebase:firebase-core:16.0.7'
}

allprojects {
  afterEvaluate { project ->

    def signProperties = rootProject.file('signing.properties')
    if (signProperties.exists()) {
      def prop = new Properties()
      prop.load(new FileInputStream(signProperties))
      def filepath = prop["keys"]
      def keyProperties = file(filepath)
      if (keyProperties.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(keyProperties))
        def keystorePath = props['keystore']
        def releaseConfig = android.signingConfigs['release']
        releaseConfig.storeFile = file(keystorePath)
        releaseConfig.storePassword = props['storePass']
        releaseConfig.keyAlias = props['keyAlias']
        releaseConfig.keyPassword = props['keyPass']
      }
    }
  }
}

afterEvaluate {
  initFabricPropertiesIfNeeded()
}

/**
 * For CI because otherwise the build will fail on the fabric plugin*/
def initFabricPropertiesIfNeeded() {
  def propertiesFile = file('fabric.properties')
  if (!propertiesFile.exists()) {
    def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
    ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
      entry(key: "apiSecret", value: crashlyticsBuildSecret)
      entry(key: "apiKey", value: crashlyticsApikey)
    }
  }
}

coveralls {
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
}

tasks.coveralls {
  dependsOn 'jacocoTestReport'
  onlyIf { System.env.'CI' }
}

repositories {
  mavenCentral()
}

jacoco {
  toolVersion = "0.8.2"
}

androidExtensions {
  experimental = true
}

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:0.30.0"
  // additional 3rd party ruleset(s) can be specified here
  // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
  // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
  if (variant.name != 'play') {
    project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled = false
  }
}
