import java.text.SimpleDateFormat

plugins {
  id "com.google.protobuf" version "0.8.12"
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'kotlin-parcelize'
  id 'androidx.navigation.safeargs.kotlin'
  id 'idea'
  id 'jacoco'
  id 'com.github.kt3k.coveralls'
  id 'com.google.firebase.firebase-perf'
}


static buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def gitHash() {
  return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}

def version = "1.4.0"
def version_code = 120

android {
  compileSdkVersion 30
  buildToolsVersion "29.0.3"

  buildFeatures {
    dataBinding true
  }

  defaultConfig {
    applicationId "com.kelsos.mbrc"
    minSdkVersion 23
    targetSdkVersion 30
    versionCode version_code
    versionName version
    buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

    // Enabling multidex support.
    multiDexEnabled true

    testInstrumentationRunner 'com.kelsos.mbrc.MockTestRunner'
    testApplicationId "com.kelsos.mbrc.test"
    vectorDrawables.useSupportLibrary = true
  }

  testOptions {
    unitTests.returnDefaultValues = true
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }

  compileOptions {
    // Flag to enable support for the new language APIs
    coreLibraryDesugaringEnabled true
    // Sets Java compatibility to Java 8
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }

  dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.0'
  }

  signingConfigs {
    release {
      storeFile file('placeholder')
      keyAlias "placeholder"
      storePassword "placeholder"
      keyPassword "placeholder"
    }
  }

  flavorDimensions "basic"

  buildTypes {
    release {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
      testCoverageEnabled true

      buildConfigField "String", "GIT_SHA", "\"debug_build\""
      buildConfigField "String", "BUILD_TIME", "\"debug_build\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
    }
  }

  productFlavors {
    play {
      ext.enableCrashlytics = true
      apply plugin: 'com.google.gms.google-services'
      apply plugin: 'com.google.firebase.crashlytics'
    }

    github {
      ext.enableCrashlytics = false
    }
  }


  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude "**/module-info.class"
    pickFirst("META-INF/atomicfu.kotlin_module")
  }

  lintOptions {
    abortOnError false
    lintConfig file("lint.xml")
    xmlReport false
    htmlReport true
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }

  dexOptions {
    javaMaxHeapSize "2G"
  }

  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${defaultConfig.applicationId}-${variant.flavorName}-${variant.versionCode}-v${variant.versionName}.apk"
    }
  }

  sourceSets {
    androidTest.java.srcDirs += "src/androidTest/kotlin"
    test.java.srcDirs += 'src/test/kotlin'
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    play.java.srcDirs += 'src/play/kotlin'
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

ext {
  espresso_version = "3.0.2"
  junit_version = "4.12"
  mockito_version = "2.19.0"
  robolectric_version = "3.8"
  truth_version = "0.41"
  leakcanary_version = "2.5"
}

dependencies {
  implementation project(path: ':changelog')
  implementation 'androidx.viewpager2:viewpager2:1.0.0'

  //Instrumentation Test dependencies
  androidTestImplementation 'androidx.annotation:annotation:1.2.0-beta01'

  androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'
  androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha3', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0-alpha03'
  androidTestImplementation "junit:junit:${junit_version}"
  androidTestImplementation 'androidx.test:runner:1.3.1-alpha03'
  androidTestImplementation 'androidx.test:rules:1.3.1-alpha03'

  androidTestUtil 'androidx.test:orchestrator:1.3.1-alpha03'

  androidTestImplementation "org.mockito:mockito-core:${mockito_version}"
  androidTestImplementation "org.mockito:mockito-android:${mockito_version}"

  androidTestImplementation 'androidx.annotation:annotation:1.1.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test:rules:1.3.0'
  androidTestImplementation 'com.linkedin.testbutler:test-butler-library:2.1.0'

  // Test dependencies
  testImplementation "junit:junit:${junit_version}"
  testImplementation "org.mockito:mockito-core:${mockito_version}"
  testImplementation "org.robolectric:robolectric:${robolectric_version}"
  testImplementation "org.robolectric:shadows-multidex:${robolectric_version}"
  testImplementation "com.google.truth:truth:${truth_version}"

  //The android library only will fail the robolectric tests
  testImplementation 'org.threeten:threetenbp:1.5.0'

  implementation 'com.google.android.material:material:1.3.0-alpha03'

  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.media:media:1.2.1'

  implementation 'androidx.core:core-ktx:1.5.0-beta01'

  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'androidx.recyclerview:recyclerview:1.1.0'

  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
  kapt "androidx.lifecycle:lifecycle-compiler:2.2.0"

  implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha02'
  implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha02'

  implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-alpha02'
  implementation 'android.arch.navigation:navigation-ui:1.0.0-alpha02'

  implementation "androidx.paging:paging-runtime-ktx:2.1.2";

  kapt 'androidx.databinding:databinding-compiler:4.1.2'

  implementation 'androidx.room:room-runtime:2.2.6';
  implementation "androidx.room:room-ktx:2.2.6"
  kapt 'androidx.room:room-compiler:2.2.6'

  implementation 'com.squareup.moshi:moshi:1.11.0'
  kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.11.0'

  implementation 'org.koin:koin-android:0.9.3'
  implementation 'org.koin:koin-android-architecture:0.9.3'

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
  implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-rx2', version: '1.4.2'

  implementation 'androidx.multidex:multidex:2.0.1'
  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'androidx.multidex:multidex:2.0.1'

  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

  implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

  implementation "androidx.datastore:datastore-core:1.0.0-alpha02"
  implementation "com.google.protobuf:protobuf-javalite:3.14.0"


  debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakcanary_version}"

  // Import the BoM for the Firebase platform
  playImplementation platform('com.google.firebase:firebase-bom:25.12.0')

  // Declare the dependencies for the Crashlytics and Analytics libraries
  // When using the BoM, you don't specify versions in Firebase library dependencies
  playImplementation 'com.google.firebase:firebase-crashlytics-ktx'
  playImplementation 'com.google.firebase:firebase-analytics-ktx'

  playImplementation 'com.google.firebase:firebase-perf-ktx'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.10.0"
  }

  // Generates the java Protobuf-lite code for the Protobufs in this project. See
  // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
  // for more information.
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}


allprojects {
  afterEvaluate { project ->

    def signProperties = rootProject.file('signing.properties')
    if (signProperties.exists()) {
      def prop = new Properties()
      prop.load(new FileInputStream(signProperties))
      def filepath = prop["keys"]
      def keyProperties = file(filepath)
      if (keyProperties.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(keyProperties))
        def keystorePath = props['keystore']
        def releaseConfig = android.signingConfigs['release']
        releaseConfig.storeFile = file(keystorePath)
        releaseConfig.storePassword = props['storePass']
        releaseConfig.keyAlias = props['keyAlias']
        releaseConfig.keyPassword = props['keyPass']
      }
    }
  }
}

coveralls {
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
}

tasks.coveralls {
  dependsOn 'jacocoTestReport'
  onlyIf { System.env.'CI' }
}

repositories {
  mavenCentral()
}

jacoco {
  toolVersion = "0.8.1"
}

//task jacocoTestReport(type: JacocoReport,
//  dependsOn: ['testGithubDebugUnitTest'/*, 'createGithubDebugCoverageReport'*/]) {
//  group = "Reporting"
//  description "Generate jacoco coverage reports"
//
//  reports {
//    xml.enabled = true
//    html.enabled = true
//  }
//
//  def fileFilter = [
//    '**/R.class',
//    '**/R$*.class',
//    '**/BuildConfig.*',
//    '**/Manifest*.*',
//    '**/*Test*.*',
//    '**/FactoryRegistry.*',
//    '**/*_ViewBinding*',
//    '**/*MemberInjector*',
//    '**/*$$Factory*.*',
//    '**/MemberInjectorRegistry.*',
//    '**/**_Table.*',
//    '**/GeneratedDatabaseHolder.*',
//    '**/*_Database.*',
//    '**/*$*$*.*', // Anonymous classes generated by kotlin
//    'android/**/*.*'
//  ]
//  def debugTree = fileTree(
//    dir: "${buildDir}/intermediates/classes/github/debug",
//    excludes: fileFilter
//  )
//  def kotlinTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/githubDebug")
//  def mainSrc = "${project.projectDir}/src/main/kotlin"
//
//  sourceDirectories = files([mainSrc])
//  classDirectories = files([debugTree, kotlinTree])
//  executionData = fileTree(dir: "$buildDir", includes: [
//    "**/*.exec",
//    "**/*.ec"
//  ])
//}

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:0.24.0"
  // additional 3rd party ruleset(s) can be specified here
  // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
  // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
  if (variant.name != 'play') {
    project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled = false
  }
}
